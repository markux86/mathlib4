/-
Copyright (c) 2024 Mitchell Horner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mitchell Horner
-/
import Mathlib.Analysis.Asymptotics.AsymptoticEquivalent
import Mathlib.Combinatorics.SimpleGraph.Extremal.Basic
import Mathlib.Data.Nat.Choose.Cast

/-!
# Tur√°n density

This modules defines the **Tur√°n density** of a simple graph.

## Main definitions

* `SimpleGraph.turanDensity H` is the **Tur√°n density** of the simple graph `H`, defined as the
  limit of `extremalNumber (Fin n) H / n.choose 2` as `n` approaches `‚àû`.

* `SimpleGraph.tendsto_turanDensity` is the proof that `SimpleGraph.turanDensity` is well-defined.

* `SimpleGraph.isEquivalent_extremalNumber` is the proof that `extremalNumber (Fin n) H` is
  asymptotically equivalent to `turanDensity H * n.choose 2` as `n` approaches `‚àû`.
-/


open Fintype Topology Asymptotics

namespace SimpleGraph

variable {V : Type*} {G H : SimpleGraph V}

section TuranDensity

/-- If `G` is an `n+1`-vertex `H`-free simple graph, then `n`-vertex induced subgraphs of `G` have
at most `extremalNumber (Fin n) H` number of edges. -/
lemma card_edgeFinset_induce_of_free_le_extremalNumber
    {n : ‚Ñï} (G : SimpleGraph (Fin (n+1))) [DecidableRel G.Adj] (h : H.Free G) (x : Fin (n+1)) :
    (G.induce {x}·∂ú).edgeFinset.card ‚â§ extremalNumber (Fin n) H := by
  have h_card_eq : card (‚Üë{x}·∂ú : Set (Fin (n+1))) = n := by
    simp_rw [‚ÜêSet.toFinset_card, Set.toFinset_compl, Set.toFinset_singleton, Finset.card_compl,
      card_fin, Finset.card_singleton, Nat.add_sub_cancel]
  rw [‚ÜêextremalNumber_eq_extremalNumber_of_iso (equivFinOfCardEq h_card_eq) Iso.refl]
  apply le_extremalNumber
  contrapose! h
  rw [not_not] at h ‚ä¢
  exact h.trans ‚ü®SubgraphIso.induce G {x}·∂ú‚ü©

lemma extremalNumber_div_choose_two_succ_le {n : ‚Ñï} (hn : 2 ‚â§ n) :
    (extremalNumber (Fin (n+1)) H / (n+1).choose 2 : ‚Ñù)
      ‚â§ (extremalNumber (Fin n) H / n.choose 2 : ‚Ñù) := by
  rw [div_le_iff‚ÇÄ <| Nat.cast_pos.mpr <| Nat.choose_pos (by linarith),
    extremalNumber_le_iff_of_nonneg (Fin (n+1)) H (by positivity)]
  intro G _ h
  rw [mul_comm, ‚Üêmul_div_assoc, le_div_iff‚ÇÄ' <| Nat.cast_pos.mpr <| Nat.choose_pos hn]
  -- double-counting vertices not in edges
  let s := (Finset.univ √óÀ¢ G.edgeFinset).filter fun (v, e) ‚Ü¶ v ‚àâ e
  -- counting over vertices
  have h_vertices : s.card ‚â§ extremalNumber (Fin n) H * (n+1) := by
    conv_rhs =>
      rw [‚Üêcard_fin (n+1), ‚ÜêFinset.card_univ, mul_comm, ‚Üêsmul_eq_mul, ‚ÜêFinset.sum_const]
    simp_rw [Finset.card_filter _ _, Finset.sum_product, ‚ÜêFinset.card_filter]
    apply Finset.sum_le_sum
    simp_rw [Finset.mem_univ, forall_const, ‚ÜêedgeFinset_deleteIncidenceSet_eq_filter,
      ‚Üêcard_edgeFinset_induce_compl_singleton G]
    exact card_edgeFinset_induce_of_free_le_extremalNumber G h
  -- counting over edges
  have h_edges : s.card = G.edgeFinset.card * (n-1) := by
    simp_rw [Finset.card_filter _ _, Finset.sum_product_right, ‚ÜêFinset.card_filter _ _,
      Sym2.mem_toFinset, Finset.filter_not, Finset.filter_mem_eq_inter,
      ‚ÜêFinset.sum_attach G.edgeFinset, Finset.univ_inter, ‚ÜêFinset.compl_eq_univ_sdiff,
      Finset.card_compl, card_fin, card_toFinset_mem_edgeFinset, Finset.sum_const,
      Finset.card_attach, smul_eq_mul, Nat.succ_sub_succ_eq_sub]
  rw [mul_comm (n.choose 2 : ‚Ñù) _, Nat.cast_choose_two, ‚Üêmul_div_assoc,
    mul_comm ((n+1).choose 2 : ‚Ñù) _, Nat.cast_choose_two, ‚Üêmul_div_assoc,
    div_le_div_iff_of_pos_right zero_lt_two, ‚ÜêNat.cast_pred (by positivity),
    ‚ÜêNat.cast_pred (by positivity), mul_comm (n : ‚Ñù) _, ‚Üêmul_assoc, ‚Üêmul_assoc,
    show n + 1 - 1 = n from Nat.pred_succ n, mul_le_mul_right (by positivity), ‚ÜêNat.cast_mul,
    ‚ÜêNat.cast_mul, Nat.cast_le]
  rwa [h_edges] at h_vertices

/-- The limit `extremalNumber (Fin n) H / n.choose 2` as `n` approaches `‚àû` exists. -/
lemma exists_tendsto_extremalNumber_div_choose_two (H : SimpleGraph V) :
    ‚àÉ x, Filter.Tendsto (fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H / n.choose 2 : ‚Ñù))
      Filter.atTop (ùìù x) := by
  let f := fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H / n.choose 2 : ‚Ñù)
  suffices h : ‚àÉ x, Filter.Tendsto (fun (n : ‚Ñï) ‚Ü¶ f (n + 2)) Filter.atTop (ùìù x) by
    simpa [Filter.tendsto_add_atTop_iff_nat 2] using h
  -- limit of antitone sequence bounded from below is infimum
  use ‚®Ö n, f (n + 2)
  apply tendsto_atTop_ciInf
  ¬∑ apply antitone_nat_of_succ_le
    intro n
    apply extremalNumber_div_choose_two_succ_le
    rw [le_add_iff_nonneg_left]
    exact Nat.zero_le n
  ¬∑ simp_rw [bddBelow_def, Set.mem_range,
      forall_exists_index, forall_apply_eq_imp_iff]
    use 0
    intro n
    positivity

/-- The **Tur√°n density** of a simple graph `H` is the limit of
`extremalNumber (Fin n) H / n.choose 2` as `n` approaches `‚àû`.

See `SimpleGraph.tendsto_turanDensity` for proof of existence. -/
noncomputable def turanDensity (H : SimpleGraph V) :=
  limUnder Filter.atTop
    fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H / n.choose 2 : ‚Ñù)

/-- The **Tur√°n density** of a simple graph `H` is well-defined. -/
theorem tendsto_turanDensity (H : SimpleGraph V) :
    Filter.Tendsto (fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H / n.choose 2 : ‚Ñù))
      Filter.atTop (ùìù (turanDensity H)) := by
  have ‚ü®_, h‚ü© := exists_tendsto_extremalNumber_div_choose_two H
  rwa [‚ÜêFilter.Tendsto.limUnder_eq h] at h

/-- `extremalNumber (Fin n) H` is asymptotically equivalent to `turanDensity H * n.choose 2` as `n`
approaches `‚àû`. -/
theorem isEquivalent_extremalNumber {H : SimpleGraph V} (h : turanDensity H ‚â† 0) :
    (fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H : ‚Ñù))
      ~[Filter.atTop] (fun (n : ‚Ñï) ‚Ü¶ (turanDensity H * n.choose 2 : ‚Ñù)) := by
  have hz : ‚àÄ·∂† (x : ‚Ñï) in Filter.atTop, turanDensity H * x.choose 2 ‚â† 0 := by
    rw [Filter.eventually_atTop]
    use 2
    intro n hn
    field_simp [h, Nat.choose_eq_zero_iff, hn]
  rw [isEquivalent_iff_tendsto_one hz]
  have hœÄ := tendsto_turanDensity H
  apply Filter.Tendsto.const_mul (1 / (turanDensity H) : ‚Ñù) at hœÄ
  simp_rw [one_div_mul_cancel h] at hœÄ
  convert hœÄ
  field_simp [Pi.div_apply]

end TuranDensity

end SimpleGraph
